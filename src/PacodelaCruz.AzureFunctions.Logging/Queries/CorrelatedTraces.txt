//Properties used with Azure Functions structure logging are logged as customDimensions.prop__{name}
//To filter events created by our custom structured logging, get only those traces where EventId > 1
//To correlate different checkpoints, we use the CorrelationId
traces 
| sort by timestamp desc 
| where customDimensions.EventId > 1 
| where customDimensions.prop__CheckPoint == 'Publisher'
| project  Level = customDimensions.LogLevel 
           , PublisherEventId = customDimensions.EventId
           , PublisherEventDescription = customDimensions.prop__EventDescription
           , PublisherCheckPoint = customDimensions.prop__CheckPoint
           , PublisherTimestamp = timestamp 
           , EntityType = customDimensions.prop__EntityType
           , EntityId = customDimensions.prop__EntityId
           , PublisherStatus = customDimensions.prop__Status
           , CorrelationId = tostring(customDimensions.prop__CorrelationId)
           , PublisherDescription = customDimensions.prop__Description
| join kind = leftouter (     
    traces 
    | sort by timestamp desc 
    | where customDimensions.EventId > 1
    | where customDimensions.prop__CheckPoint == 'Subscriber'
    | project  Level = customDimensions.LogLevel 
            , SubscriberEventId = customDimensions.EventId
            , SubscriberEventDescription = customDimensions.prop__EventDescription
            , SubscriberCheckPoint = customDimensions.prop__CheckPoint
            , SubscriberStatus = customDimensions.prop__Status
            , SubscriberDescription = customDimensions.prop__Description   
            , CorrelationId = tostring(customDimensions.prop__CorrelationId)
    ) on CorrelationId
| project EntityType
        , EntityId 
        , PublisherEventId 
        , SubscriberEventId 
        , PublisherStatus 
        , SubscriberStatus 
        , PublisherTimestamp 
